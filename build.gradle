plugins {
    id 'java-library'
    id 'me.qoomon.git-versioning' version '6.4.4'
}

java {
    toolchain {
      languageVersion.set(JavaLanguageVersion.of(file(".java-version").text.trim()))
    }
}

group = 'net.omerc'
version = '0.0.0-SNAPSHOT'
gitVersioning.apply {
    refs {
        branch('.+') {
            version = '${ref}-SNAPSHOT'
        }
        tag('v(?<version>.*)') {
            version = '${ref.version}'
        }
    }

    // optional fallback configuration in case of no matching ref configuration
    rev {
        version = '${commit.short}'
    }  
}


repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation 'net.portswigger.burp.extensions:montoya-api:2025.6'
    implementation 'com.thoughtworks.xstream:xstream:1.4.21'
}

sourceSets {
    example {
        java {
            srcDirs = ['src/example/java']
        }
    }
}

task exampleJar(type: Jar) {
    from sourceSets.example.output
    archiveBaseName	= 'example'
}

task runExample(type: JavaExec) {
    dependsOn exampleJar
    classpath = sourceSets.example.runtimeClasspath
    mainClass = 'example.Client'
}

jar {
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

